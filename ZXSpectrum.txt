100 REM BASIC Month: Terraspin
110 REM http://reddit.com/RetroBattlestations
120 REM written by FozzTexx - ZX Spectrm port by mattinx

130 REM Sinclair BASIC does't have bitwise operators so they are
140 REM simulated. User defined function FN O checks if a number is odd
150 REM by checking the LSB. Bit shifting is simulated by dividing by a
160 REM multiple of two. A bitwise AND is don in the subroutine at 5500

200 REM === Initialize variables
210 LET SW=256: LET SH=176: LET XS=SW/1000: LET YS=(SH*4)/(1000*3)
220 LET SW=SW-1: LET SH=SH-1
230 DIM S(10)
240 DEF FN O(U)=(INT U)/2-INT (U/2)>0

250 REM === Set turtle start at centre of screen poining up, pen up
260 LET TX=500: LET TY=SH/YS/2: LET TA=90: LET TP=0

300 REM === Get command to execute
310 LET M$="5 ( D 12 ( 210 M 5 ( 30 T 10 M ) ) U 100 T 150 M )"

480 REM === Clear screen, set border white, background white, and ink black
490 CLS : BORDER 7: PAPER 7: INK 0

500 REM === Command interpreter
510 LET IP=1: LET SP=0
520 LET C$=M$(IP)
530 IF C$=" " THEN LET IP=IP+1: GO TO 520
540 IF C$>="-" AND C$<="9" THEN LET V=VAL M$(IP): GO SUB 1010: GO SUB 1210
550 IF C$="(" THEN LET V=IP: GO SUB 1010
560 IF C$=")" THEN GO SUB 2010
570 IF C$="M" THEN GO SUB 2510
580 IF C$="T" THEN GO SUB 3010
590 IF C$="U" THEN GO SUB 3510
600 IF C$="D" THEN GO SUB 4010
610 LET IP=IP+1
620 IF IP>LEN M$ THEN STOP
630 GO TO 520

1000 REM === Push onto stack
1010 LET SP=SP+1: LET S(SP)=V
1020 RETURN

1100 REM === Pop from stack
1110 LET V=S(SP): LET SP=SP-1
1120 RETURN

1200 REM === Skip over number
1210 LET IP=IP+1
1220 IF IP>LEN M$ THEN GO TO 1250
1230 LET D$=M$(IP)
1240 IF D$>="-" AND D$<="9" THEN GO SUB 1110: LET V=V*10+VAL D$: GO SUB 1010: GO TO 1210
1250 LET IP=IP-1
1260 RETURN

1500 REM === Plot a Line
1510 PLOT X1,Y1: DRAW X2-X1,Y2-Y1
1520 RETURN

2000 REM === Loop instruction end
2010 GO SUB 1110: LET BP=V
2020 GO SUB 1110: LET LR=V
2030 LET LR=LR-1
2040 IF LR<1 THEN RETURN
2050 LET V=LR: GO SUB 1010
2060 LET V=BP: GO SUB 1010
2070 LET IP=BP
2080 RETURN

2500 REM === Move
2510 GO SUB 1110
2520 LET LX=V*COS ((360-TA)*PI/180): LET LY=V*SIN ((360-TA)*PI/180)
2530 IF TP>0 THEN LET X1=TX: LET Y1=TY: LET X2=X1+LX: LET Y2=Y1+LY: GO SUB 4510
2540 LET TX=TX+LX: LET TY=TY+LY
2550 RETURN

3000 REM === Turn
3010 GO SUB 1110
3020 LET TA=TA+V
3030 IF TA<0 THEN LET TA=TA+360: GO TO 3030
3040 IF TA>=360 THEN LET TA=TA-360: GO TO 3040
3050 RETURN

3500 REM === Pen up
3510 LET TP=0
3520 RETURN

4000 REM === Pen down
4010 LET TP=1
4020 RETURN

4500 REM === Draw line, clipping if needed
4510 LET X1=X1*XS: LET X2=X2*XS: LET Y1=Y1*YS: LET Y2=Y2*YS
4520 LET X=X1: LET Y=Y1: GO SUB 5010: LET C1=C
4530 LET X=X2: LET Y=Y2: GO SUB 5010: LET C2=C
4540 IF C1=0 AND C2=0 THEN GO SUB 1510: RETURN
4550 GO SUB 5510: IF C THEN RETURN
4560 LET C=C1: IF C=0 THEN LET C=C2
4570 IF FN O(C) THEN LET X=X1+(X2-X1)*(SH-Y1)/(Y2-Y1): LET Y=SH: GO TO 4610
4580 IF FN O(C/2) THEN LET X=X1+(X2-X1)*(0-Y1)/(Y2-Y1): LET Y=0: GO TO 4610
4590 IF FN O(C/4) THEN LET Y=Y1+(Y2-Y1)*(SW-X1)/(X2-X1): LET X=SW: GO TO 4610
4600 IF FN O(C/8) THEN LET Y=Y1+(Y2-Y1)*(0-X1)/(X2-X1): LET X=0: GO TO 4610
4610 IF C=C1 THEN LET X1=X: LET Y1=Y: GO TO 4630
4620 LET X2=X: LET Y2=Y
4630 GO TO 4520

5000 REM === Calculate region code
5010 LET C=0
5020 IF Y>SH THEN LET C=C+1
5030 IF Y<0 THEN LET C=C+2
5040 IF X>SW THEN LET C=C+4
5050 IF X<0 THEN LET C=C+8
5060 RETURN

5500 REM === Bitwise AND of C1 and C2
5510 LET C=0: LET EP=1: FOR I=1 TO 4
5520 LET C=C+FN O(C1/EP)*FN O(C2/EP)*EP
5530 LET EP=EP*2: NEXT I
5540 RETURN
